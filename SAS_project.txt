/*setting libname as r*/

Libname r "S:\Data";
run;

/*setting libname of actual XPT file*/

Libname XPTfile xport 'S:\Data\LLCP2014.XPT';
run;

/*data step reads it in and unpacks it into libname mapped to r*/

data r.BRFSS_1;
    set XPTfile.LLCP2014;
run;

/*verifying success*/

proc contents data=r.BRFSS_1;
run;


data r.BRFSS_2;
	set r.BRFSS_1 (keep =
		VETERAN3 
		ASTHMA3
		DIABETE3
		SLEPTIM1
		_AGE_G
		SMOKE100
		SMOKDAY2
		SEX
		_HISPANC
		_MRACE1
		MARITAL
		GENHLTH
		HLTHPLN1
		EDUCA
		INCOME2
		_BMI5CAT
		EXERANY2);
run;

proc contents data = r.BRFSS_2;
run;

/*If you want to keep most of the variables, then use
the drop command instead*/

data Drop_Example;
	set r.BRFSS_1 (drop =
		VETERAN3 
		ASTHMA3
		DIABETE3
		SLEPTIM1
		_AGE_G
		SMOKE100
		SMOKDAY2
		SEX
		_HISPANC
		_MRACE1
		MARITAL
		GENHLTH
		HLTHPLN1
		EDUCA
		INCOME2
		_BMI5CAT
		EXERANY2);
run; 

Proc Contents data=Drop_Example;
run;

/*proc freq without options*/

proc freq data=r.BRFSS_2;
	tables VETERAN3;
run;

/*Checking exclusions with the missing option*/

proc freq data=r.BRFSS_2;
	tables VETERAN3 / missing;
	tables DIABETE3 / missing;
	tables SLEPTIM1 / missing;
	tables ASTHMA3 / missing;
run;

/*applying veteran exclusion*/

data r.BRFSS_3;
	set r.BRFSS_2;
	if VETERAN3 = 1;
run;

/*alternative using delete option*/

data Delete_Example;
	set r.BRFSS_2;
	if VETERAN3 ne 1 then delete;
run;

/*applying diabetes exclusion*/

data r.BRFSS_4;
	set r.BRFSS_3;
	if DIABETE3 in (1, 2, 3, 4);
run;

/*applying asthma exclusion*/

data r.BRFSS_5;
	set r.BRFSS_4;
	if ASTHMA3 in (1, 2);
run;

/*Keeping only those with SLEPTIM1 <77
because 1 through 24 are valid, and 77 and 99 are not.*/

data r.BRFSS_6;
	set r.BRFSS_5;
	if SLEPTIM1 < 77;
run;

/*alternative using other operators*/

data Other_Operators;
	set r.BRFSS_5;
	if SLEPTIM1 ge 77 then delete;
run;
/*creating recode of DIABETE3 in DIABETE4*/

data r.BRFSS_7;
	set r.BRFSS_6;

	DIABETE4 = 9;
	
	if DIABETE3 in (1, 2) 
		then DIABETE4 = 1;
	
	if (DIABETE3 = 3 | DIABETE3 = 4)
		then DIABETE4 = 2;

run;

/*Checking recode*/

/*proc freq without list option*/

proc freq data=r.BRFSS_7;
	table DIABETE3 * DIABETE4 / missing;
run;

/*Much easier to read with list option!*/

proc freq data=r.BRFSS_7;
	table DIABETE3 * DIABETE4 / list missing;
run;
/*first create NEVERSMK, then create grouping variable
based on NEVERSMK and SMOKDAY2*/

data r.BRFSS_8;
	set r.BRFSS_7;

	NEVERSMK = 0;

	if SMOKE100 = 2
		then NEVERSMK = 1;

	SMOKGRP = 9;

	if (SMOKDAY2 = 1 | SMOKDAY2 = 2)
		then SMOKGRP = 1;
	if (SMOKDAY2 = 3 | NEVERSMK = 1)
		then SMOKGRP = 2;
	
run;

/*Checking recode for inconsistencies*/

proc freq data=r.BRFSS_8;
	table NEVERSMK * SMOKE100 / list missing;
	table SMOKGRP * SMOKDAY2 / list missing;
run;
/*creating rest of grouping variables*/

data r.BRFSS_9;
	set r.BRFSS_8;

	MARGRP = 9;

	if (MARITAL = 1 | MARITAL = 6)
		then MARGRP = 1;
	if MARITAL in (2, 3, 4)
		then MARGRP = 2;
	if MARITAL = 5
		then MARGRP = 3;

	EDGROUP = 9;

	if EDUCA in (1, 2, 3)
		then EDGROUP = 1;
	if EDUCA = 4
		then EDGROUP = 2;
	if EDUCA = 5
		then EDGROUP = 3;
	if EDUCA = 6
		then EDGROUP = 4;

	GENHLTH2 = GENHLTH;

	if GENHLTH2 not in (1, 2, 3, 4, 5)
		then GENHLTH2 = 9;

	INCOME3 = INCOME2;

	if INCOME3 not in (1, 2, 3, 4, 5, 6, 7, 8)
		then INCOME3 = 9;

	RACEGRP = _MRACE1;

	if _MRACE1 = 7
		then RACEGRP = 6;
	if _MRACE1 not in (1, 2, 3, 4, 5, 6, 7)
		then RACEGRP = 9;

		
	EXERANY3 = EXERANY2;

	if EXERANY2 not in (1, 2)
		Then EXERANY3 = 9;

	HLTHPLN2 = HLTHPLN1;

	if HLTHPLN1 not in (1, 2)
		Then HLTHPLN2 = 9;

	BMICAT = _BMI5CAT;

	if BMICAT not in (1, 2, 3, 4)
		then BMICAT = 9;

run;

proc freq data=r.BRFSS_9;
	table MARGRP * MARITAL / list missing;
	table EDGROUP * EDUCA / list missing;
	table GENHLTH2 * GENHLTH / list missing;
	table INCOME3 * INCOME2 / list missing;
	table RACEGRP * _MRACE1 / list missing;
	table EXERANY3 * EXERANY2 / list missing;
	table HLTHPLN2 * HLTHPLN1 / list missing;
	table BMICAT * _BMI5CAT / list missing;
	table DIABETE3 * DIABETE4 / list missing;
run;


/*indicator variables for age group*/

data r.BRFSS_9;
	set r.BRFSS_9;

	AGE2 = 0;
	if _AGE_G = 2
		then AGE2 = 1;

	AGE3 = 0;
	if _AGE_G = 3
		then AGE3 = 1;

	AGE4 = 0;
	if _AGE_G = 4
		then AGE4 = 1;

	AGE5 = 0;
	if _AGE_G = 5
		then AGE5 = 1;

	AGE6 = 0;
	if _AGE_G = 6
		then AGE6 = 1;

run;

proc freq data=r.BRFSS_9;
	tables AGE2*_AGE_G/list missing;
	tables AGE3*_AGE_G/list missing;
	tables AGE4*_AGE_G/list missing;
	tables AGE5*_AGE_G/list missing;
	tables AGE6*_AGE_G/list missing;
run;

/*indicator variables for income*/

data r.BRFSS_10;
	set r.BRFSS_9;

	INC1 = 0;
	if INCOME3 = 1
		then INC1 = 1;

	INC2 = 0;
	if INCOME3 = 2
		then INC2 = 1;

	INC3 = 0;
	if INCOME3 = 3
		then INC3 = 1;

	INC4 = 0;
	if INCOME3 = 4
		then INC4 = 1;

	INC5 = 0;
	if INCOME3 = 5
		then INC5 = 1;

	INC6 = 0;
	if INCOME3 = 6
		then INC6 = 1;

	INC7 = 0;
	if INCOME3 = 7
		then INC7 = 1;

run;

proc freq data=r.BRFSS_10;
	tables INC1*INCOME3/list missing;
	tables INC2*INCOME3/list missing;
	tables INC3*INCOME3/list missing;
	tables INC4*INCOME3/list missing;
	tables INC5*INCOME3/list missing;
	tables INC6*INCOME3/list missing;
	tables INC7*INCOME3/list missing;
run;


/*indicator variables for race*/

data r.BRFSS_11;
	set r.BRFSS_10;

	BLACK = 0;
	if RACEGRP = 2
		then BLACK = 1;

	ASIAN = 0;
	if RACEGRP = 4
		then ASIAN = 1;

	OTHRACE = 0;
	if RACEGRP in (3, 5, 6, 7)
		then OTHRACE = 1;

run;

proc freq data=r.BRFSS_11;
	tables BLACK*RACEGRP/list missing;
	tables ASIAN*RACEGRP/list missing;
	tables OTHRACE*RACEGRP/list missing;
run;

/*indicator variables for marital status*/

data r.BRFSS_13;
	set r.BRFSS_11;

	NEVERMAR = 0;
	if MARGRP = 3
		then NEVERMAR = 1;

	FORMERMAR = 0;
	if MARGRP = 2
		then FORMERMAR = 1;

run;

proc freq data=r.BRFSS_13;
	tables NEVERMAR*MARGRP/list missing;
	tables FORMERMAR*MARGRP/list missing;
run;

/*indicator variables for education*/

data r.BRFSS_14;
	set r.BRFSS_13;

	LOWED = 0;
	if EDGROUP in (1,2)
		then LOWED = 1;

	SOMECOLL  = 0;
	if EDGROUP = 3
		then SOMECOLL  = 1;

run;

proc freq data=r.BRFSS_14;
	tables LOWED*EDGROUP/list missing;
	tables SOMECOLL*EDGROUP/list missing;
run;

/*indicator variables for BMI*/

data r.BRFSS_15;
	set r.BRFSS_14;

	UNDWT = 0;
	if BMICAT = 1
		then UNDWT = 1;

	OVWT = 0;
	if BMICAT = 3
		then OVWT = 1;

	OBESE = 0;
	if BMICAT = 4
		then OBESE = 1;

run;

proc freq data=r.BRFSS_15;
	tables UNDWT*BMICAT/list missing;
	tables OVWT*BMICAT/list missing;
	tables OBESE*BMICAT/list missing;
run;

/*indicator variables for general health*/

data r.BRFSS_16;
	set r.BRFSS_15;

	FAIRHLTH = 0;
	if GENHLTH2 = 4
		then FAIRHLTH = 1;

	POORHLTH = 0;
	if GENHLTH2 = 5
		then POORHLTH = 1;

run;

proc freq data=r.BRFSS_16;
	tables FAIRHLTH*GENHLTH2/list missing;
	tables POORHLTH*GENHLTH2/list missing;
run;

/*indicator variables for three-level grouping variables*/

data r.BRFSS_17;
	set r.BRFSS_16;
	
	ASTHMAFLAG = 0;
	if ASTHMA3 = 1
		then ASTHMAFLAG = 1;
	
	DIABFLAG = 0;
	if DIABETE4 = 1
		then DIABFLAG = 1;

	MALE = 0;
	if SEX = 1
		then MALE = 1;

	HISPANIC = 0;
	if _HISPANC = 1
		then HISPANIC = 1;

	SMOKER = 0;
	if SMOKGRP = 1
		then SMOKER = 1;

	NOEXER = 0;
	if EXERANY3 = 1
		then NOEXER = 1;

	NOPLAN = 0;
	if HLTHPLN2 = 2
		then NOPLAN = 1;

run;

proc freq data=r.BRFSS_17;
	tables ASTHMAFLAG*ASTHMA3/list missing;
	tables DIABFLAG*DIABETE3/list missing;
	tables MALE*SEX/list missing;
	tables HISPANIC*_HISPANC/list missing;
	tables SMOKER*SMOKGRP/list missing;
	tables NOEXER*EXERANY3/list missing;
	tables NOPLAN*HLTHPLN2/list missing;
run;
/*Review distribution of sleep duration*/

proc univariate data = r.BRFSS_16;
	var SLEPTIM1;
run;

/*May be helpful to look at frequencies*/

proc freq data = r.BRFSS_16;
	tables SLEPTIM1 /missing;
run;


/*Example: Create arbitrary grouping*/

data Grouping_Example;
	set r.BRFSS_16;

	SLEEPGRP = 9;

	if SLEPTIM1 le 5
		then SLEEPGRP = 1;
	if (SLEPTIM1 gt 5) & (SLEPTIM1 le 7)
		then SLEEPGRP = 2;
	if (SLEPTIM1 gt 7) & (SLEPTIM1 le 9)
		then SLEEPGRP = 3; 
	if SLEPTIM1 gt 9
		then SLEEPGRP = 4; 

	if SLEEPGRP = 1
		then SG1 = 1;
	else SG1 = 0;

	if SLEEPGRP = 2
		then SG2 = 1;
	else SG2 = 0;

	if SLEEPGRP = 3
		then SG3 = 1;
	else SG3 = 0;

	/*No SG4 - we would theoretically use SG4 as the
	reference group in a regression*/

run;

/*Checking recode*/

proc freq data=Grouping_Example;
	tables SLEPTIM1*SLEEPGRP/list missing;
	tables SLEEPGRP*SG1/list missing;
	tables SLEEPGRP*SG2/list missing;
	tables SLEEPGRP*SG3/list missing;
run;


/*SLEPTIM1 in the original dataset has ineligible values*/

proc freq data = r.BRFSS_1;
	tables SLEPTIM1 /missing;
run;

/*creating SLEPTIM2 suppressing ineligible values*/

data Clean_Example;
	set r.BRFSS_1;

	SLEPTIM2 = SLEPTIM1;

	if SLEPTIM1 ge 77
		then SLEPTIM2 = .;

run;

/*Checking recode with PROC FREQ*/

proc freq data = Clean_Example;
	tables SLEPTIM1*SLEPTIM2/list missing;
run;

/*PROC UNIVARIATE on new variable is accurate*/

proc univariate data = Clean_Example;
	var SLEPTIM2;
run;

/*Final analytic dataset*/
/*We can order by variable number (varnum) so        */
/*we can see them in the order in which they         */
/*were created.                                      */

proc contents varnum data = r.BRFSS_17;
run;

/*copy to a dataset named "analytic"*/
/*which will be used in subsequent code for analysis.*/

/*If variables need to be added to the analytic dataset,*/
/*that code should come before this code.*/

data r.analytic;
	set r.BRFSS_17;

run;
proc contents varnum data = r.analytic;
run;


/*This creates a simple stacked bar graph.*/

/*One group*/
proc gchart data=r.analytic;
	vbar DIABETE4 / discrete type=percent;
	run;
quit;

/*two groups*/
proc gchart data=r.analytic;
	vbar DIABETE4 / discrete type=percent subgroup = ASTHMA3;
	run;
quit;


/*Diabetes in a pie chart*/

proc gchart data=r.analytic;
        pie DIABETE4 / DISCRETE 
		VALUE=INSIDE
        PERCENT=INSIDE 
		SLICE=OUTSIDE;
RUN;

/*Age groups*/

proc gchart data=r.analytic;
        pie _AGE_G / DISCRETE 
		VALUE=INSIDE
        PERCENT=INSIDE 
		SLICE=OUTSIDE;
RUN;


/*Histogram of SLEPTIM1 (sleep duration) with 7 levels*/
/*in analytic dataset*/

PROC GCHART DATA=r.analytic;
      VBAR SLEPTIM1 / LEVELS=7;
RUN;

/*Histogram of _AGE80 (age) with 20 levels*/
/*in BRFSS_1*/

PROC GCHART DATA=r.BRFSS_1;
      VBAR _AGE80 / LEVELS=20;
RUN;


/*box plot of SLEPTIM1 (sleep duration)*/

PROC SGPLOT  DATA = r.analytic;
   VBOX SLEPTIM1;
run;

/*stratified by ASTHMA3 (asthma status)*/

PROC SGPLOT  DATA = r.analytic;
   VBOX SLEPTIM1 / category = ASTHMA3;
run;


/*creating SLEPTIM2 in Scatter_Example by suppressing ineligible values*/
/*for SLEPTIM1 so we can use it in a scatter plot*/

data Clean_Example;
	set r.BRFSS_1;

	SLEPTIM2 = SLEPTIM1;

	if SLEPTIM1 ge 77
		then SLEPTIM2 = .;

run;

/*scatter plot age by sleep duration*/

PROC GPLOT DATA=Clean_Example;
     PLOT SLEPTIM2*_AGE80;
RUN;

/*group scatter plot by gender*/

PROC GPLOT DATA=Clean_Example;
     PLOT SLEPTIM2*_AGE80 = SEX;
RUN;

/*one-way frequency of outcome ASTHMA3*/
/*to fill in top row of table shell*/

proc freq data = r.analytic;
	tables ASTHMA3 / missing;
run;

/*example of default output for two-way frequency*/

proc freq data = r.analytic;
	tables DIABETE4*ASTHMA3;
run;

/*Use grouping variables for two-way frequencies*/
/*to calculate estimates for the other rows*/

proc freq data = r.analytic;
	tables DIABETE4 / missing;
	tables DIABETE4*ASTHMA3 / list missing;
run;

/*One set for each confounder*/
/*Listed in order of Table 1*/

proc freq data = r.analytic;
	tables _AGE_G / missing;
	tables _AGE_G*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables SEX / missing;
	tables SEX*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables _HISPANC / missing;
	tables _HISPANC*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables RACEGRP / missing;
	tables RACEGRP*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables MARGRP / list missing;
	tables MARGRP*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables EDGROUP / missing;
	tables EDGROUP*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables INCOME3 / missing;
	tables INCOME3*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables BMICAT / missing;
	tables BMICAT*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables SMOKGRP / missing;
	tables SMOKGRP*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables EXERANY3 / missing;
	tables EXERANY3*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables HLTHPLN2 / missing;
	tables HLTHPLN2*ASTHMA3 / list missing;
run;

proc freq data = r.analytic;
	tables GENHLTH2 / missing;
	tables GENHLTH2*ASTHMA3 / list missing;
run;

/*Statistical tests for categorical table*/

/*PROC FREQ and chi-square with options to suppress extra numbers - easier to read*/
proc freq data = r.analytic;
	tables DIABETE4*ASTHMA3/nocol norow nopercent chisq;
run;

/*Multiples in one proc*/
proc freq data = r.analytic;
	tables DIABETE4*ASTHMA3/nocol norow nopercent chisq;
	tables _AGE_G*ASTHMA3/nocol norow nopercent chisq;
	tables SEX*ASTHMA3/nocol norow nopercent chisq;
	tables _HISPANC*ASTHMA3/nocol norow nopercent chisq;
	tables RACEGRP*ASTHMA3/nocol norow nopercent chisq;
	tables MARGRP*ASTHMA3/nocol norow nopercent chisq;
	tables EDGROUP*ASTHMA3/nocol norow nopercent chisq;
	tables INCOME3*ASTHMA3/nocol norow nopercent chisq;
	tables BMICAT*ASTHMA3/nocol norow nopercent chisq;
	tables SMOKGRP*ASTHMA3/nocol norow nopercent chisq;
	tables EXERANY3*ASTHMA3/nocol norow nopercent chisq;
	tables HLTHPLN2*ASTHMA3/nocol norow nopercent chisq;
	tables GENHLTH2*ASTHMA3/nocol norow nopercent chisq;
run;


proc univariate data = r.analytic;
	var SLEPTIM1;
run;

/*What happens if you don't sort first by the BY variable?*/

proc univariate data = r.analytic;
	var SLEPTIM1;
	by DIABETE4;
run;

/*sort, then stratify by diabetes status*/

proc sort data = r.analytic;
	by DIABETE4;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by DIABETE4;
run;

/*analyze other stratifications*/

proc sort data = r.analytic;
	by _AGE_G;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by _AGE_G;
run;

proc sort data = r.analytic;
	by SEX;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by SEX;
run;

proc sort data = r.analytic;
	by _HISPANC;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by _HISPANC;
run;

proc sort data = r.analytic;
	by RACEGRP;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by RACEGRP;
run;

proc sort data = r.analytic;
	by MARGRP;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by MARGRP;
run;

proc sort data = r.analytic;
	by EDGROUP;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by EDGROUP;
run;

proc sort data = r.analytic;
	by INCOME3;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by INCOME3;
run;

proc sort data = r.analytic;
	by ASTHMA3;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by ASTHMA3;
run;

proc sort data = r.analytic;
	by BMICAT;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by BMICAT;
run;

proc sort data = r.analytic;
	by SMOKGRP;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by SMOKGRP;
run;

proc sort data = r.analytic;
	by EXERANY3;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by EXERANY3;
run;

proc sort data = r.analytic;
	by HLTHPLN2;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by HLTHPLN2;
run;

proc sort data = r.analytic;
	by GENHLTH2;
proc univariate data = r.analytic;
	var SLEPTIM1;
	by GENHLTH2;
run;


/*t-tests for continuous descriptive table*/

proc ttest data = r.analytic;
	class DIABETE4;
	var SLEPTIM1;
run;

proc ttest data = r.analytic;
	class SEX;
	var SLEPTIM1;
run;

proc ttest data = r.analytic;
	class ASTHMA3;
	var SLEPTIM1;
run;

/*One-way ANOVA tests for table*/

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class _AGE_G;
	model SLEPTIM1 = _AGE_G;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class _HISPANC;
	model SLEPTIM1 = _HISPANC;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class RACEGRP;
	model SLEPTIM1 = RACEGRP;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class MARGRP;
	model SLEPTIM1 = MARGRP;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class EDGROUP;
	model SLEPTIM1 = EDGROUP;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class INCOME3;
	model SLEPTIM1 = INCOME3;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class BMICAT;
	model SLEPTIM1 = BMICAT;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class SMOKGRP;
	model SLEPTIM1 = SMOKGRP;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class EXERANY3;
	model SLEPTIM1 = EXERANY3;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class HLTHPLN2;
	model SLEPTIM1 = HLTHPLN2;
run;

proc glm data = r.analytic PLOTS(MAXPOINTS=NONE);
	class GENHLTH2;
	model SLEPTIM1 = GENHLTH2;
run;

// Linear Regression
/*Checking linearity and normality assumptions with univariate plots for SLEPTIM1*/

proc univariate data = r.analytic plots;
	var SLEPTIM1;
run;

/*Checking homoscedasticity assumptions with residual plot from PROC REG*/

proc reg data=r.analytic;
	model SLEPTIM1 = DIABFLAG;
	plot r.*p.;
run;

/*Simple linear regression Model 1*/

PROC GLM data = r.analytic;
	model SLEPTIM1 = DIABFLAG;
run;

/*Model 2 - remove INC4, add back INC5 - NEW WORKING MODEL*/

PROC GLM data = r.analytic;
	model SLEPTIM1 = DIABFLAG AGE4 AGE5 AGE6 HISPANIC BLACK ASIAN OTHRACE 
		FORMERMAR SOMECOLL ASTHMAFLAG
		OVWT OBESE SMOKER FAIRHLTH POORHLTH
		AGE3 INC5;
run;
